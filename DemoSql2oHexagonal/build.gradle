plugins {
	id 'org.springframework.boot' version '2.3.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

def jarBaseName = 'demo-sql2o-hexagonal'
def distributionFolder = 'demo-sql2o-hexagonal'
def configFiles = ['application.properties', 'cache.properties', 'database.properties', 'queue.properties', 'web-service.properties', 'log4j2.xml'] as Set
def libFiles = ['com/sql2o/hexagonal/adapter/dao/*.elsql'] as Set

jar {
	archivesBaseName = jarBaseName
	exclude configFiles
	exclude libFiles
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

configurations.all {
	exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
	exclude group: "org.springframework.boot", module: "logback-classic"
}

dependencies {
	//Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	//Spring Context Support
	implementation 'org.springframework:spring-context-support:4.2.9.RELEASE'

	//Commons DBCP
	implementation 'org.apache.commons:commons-dbcp2:2.8.0'

	//Commons IO
	implementation 'commons-io:commons-io:2.8.0'

	//Commons Lang
	implementation 'org.apache.commons:commons-lang3:3.11'

	//HttpClient
	implementation 'org.apache.httpcomponents:httpclient:4.5.12'

	//MySql
	implementation 'mysql:mysql-connector-java:8.0.21'

	//Sql2o
	implementation 'org.sql2o:sql2o:1.6.0'

	//ElSql
	implementation 'com.opengamma:elsql:1.3'

	//Jasypt
	implementation 'org.jasypt:jasypt-spring4:1.9.3'

	//Guava
	implementation 'com.google.guava:guava:30.0-jre'

	//Velocity
	implementation 'org.apache.velocity:velocity:1.7'

	//IText Core
	implementation 'com.itextpdf:itextpdf:5.5.13.2'

	//PdfHTML
	implementation 'com.itextpdf:html2pdf:3.0.2'

	//Jedis
	implementation 'redis.clients:jedis:3.3.0'

	//Gson
	implementation 'com.google.code.gson:gson:2.8.6'

	//test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-all:1.10.19'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
}

task copyFilesToPackage {
	doLast {
		copy {
			from('src/main/resources') {
				include libFiles
			}
			from 'build/libs/' + jarBaseName + '.jar'
			into 'build/' + distributionFolder + '/lib'
		}

		copy {
			from('src/main/resources') {
				include configFiles
			}
			into 'build/' + distributionFolder
		}
	}
}

task zipPackage(type: Zip, dependsOn: 'copyFilesToPackage') {
	from 'build/' + distributionFolder
	archivesBaseName = distributionFolder
}

build.dependsOn zipPackage
zipPackage.mustRunAfter check